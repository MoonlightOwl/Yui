buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

version = '0.4.7'  // do not forget to change version in Yui.kt
mainClassName = "totoro.yui.YuiKt"

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "http://dl.bintray.com/kotlin/exposed"
    }
}

configurations {
    shadow
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-compiler'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-script-util'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-script-runtime'
    compile group: 'org.kitteh.irc', name: 'client-lib', version: '4.0.0'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.11.2'
    compile group: 'com.github.kittinunf.fuel', name: 'fuel', version: '1.12.0'
    compile group: 'com.beust', name: 'klaxon', version: '2.1.4'
    shadow group: 'org.xerial', name: 'sqlite-jdbc', version: '3.21.0.1'
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'yui',
                'Implementation-Version': version,
                'Main-Class': 'totoro.yui.YuiKt'
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
task copyShadowedLibs(type: Copy) {
    from configurations.shadow
    into "$buildDir/libs"
}
tasks.fatJar.dependsOn(copyShadowedLibs)
